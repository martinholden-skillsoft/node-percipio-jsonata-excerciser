$.(
			$plaintextdescription := (
					$htmltags := /<\/?([a-z0-9]*)\b[^>]*>?/;
					$dash := /&(#8210;|#8211;|#8212;|#8213;|ndash;|mdash;|horbar;)/;
					$apos := /&(#8217;|apos;|rsquo;)/;
					$copy := /&(#169;|copy;)/;
					$tm := /&(#8482;|trade;)/;
					$registered := /&(#174;|reg;)/;
					$otherentities :=  /&(#?[\w\d]+;)/;

					$desc := localizedMetadata[0].description ? $trim(localizedMetadata[0].description) : '';
					$desc := $replace($desc, $htmltags, '');
					$desc := $replace($desc, $dash, '-');
					$desc := $replace($desc, $copy, '(c)');
					$desc := $replace($desc, $registered, '(r)');
					$desc := $replace($desc, $tm, '(tm)');
					$desc := $replace($desc, $apos, $base64decode('Jw=='));
					$desc := $replace($desc, $otherentities, '');

					$desc;
			);

			$trimmedplaintextdescription := (
					$maxlength := 3999;
					$ellipsis := '...';
                    $trimlength := $maxlength - $length($ellipsis);

					$trimmedplaintextdescription := ($length($plaintextdescription) > $maxlength) ? $substring($plaintextdescription , 0, $trimlength) & $ellipsis : $plaintextdescription;
					$trimmedplaintextdescription;
			);

			$title := (
					$title := '(' & contentType.displayLabel & ') ' & $replace((localizedMetadata[0].title), '|', '-');
					$title;
			);

			$trimmedtitle := (
					$maxlength := 300;
					$ellipsis := '...';
                    $trimlength := $maxlength - $length($ellipsis);

					$trimmedtitle := ($length($title) > $maxlength) ? $substring($title, 0, $trimlength) & $ellipsis : $title;
					$trimmedtitle;
			);

			$duration_in_seconds := (
				$input := (duration = null ? '' : duration);
				$time := $substringAfter($input, 'T');
				$hours := $contains($time, 'H') ? $substringBefore($time, 'H') : '0';
				$remainder := $substringAfter($time, 'H');
				$minutes := $contains($remainder, 'M') ? $substringBefore($remainder, 'M') : '0';
				$remainder := $substringAfter($remainder, 'M');
				$seconds := $contains($remainder, 'S') ? $substringBefore($remainder, 'S') : '0';

				$result := (($number($hours) * 3600) + ($number($minutes) * 60) + $number($seconds));
				$result;
			);

			$duration_in_hours := (
				$decplaces := 2;
				$result := $duration_in_seconds = 0 ? 0 : $round($duration_in_seconds / 3600, $decplaces);
				$result;
			);

			$transformed_data := (
          $.{
              'CPNT_ID': id,
              'CPNT_TYP_ID': 'ONLINE',
              'NOTACTIVE': lifecycle.status = 'ACTIVE' ? 'N' : 'Y',
              'CPNT_TITLE': $trimmedtitle,
              'CPNT_DESC': $trimmedplaintextdescription,
              'CMPL_STAT_ID': 'ONLINE-COMPL',
              'CREDIT_HRS': $duration_in_hours,
              'SHOW_IN_CATALOG': 'Y',
              'CATALOG_1': 'EXTERNAL',
              'APP_ID': id,
              'APP_TITLE': $trimmedtitle,
              'APP_DESCRIPTION' : $trimmedplaintextdescription,
              'BUILD_COMPANY': 'Skillsoft',
              'CONTENT_ONLINE': 'Y',
              'LAUNCH_TYPE': 3,
              'PRIMARY_PARAM': link,
              'ITEM_ONLINE': 'Y',
              'MODULE_NAME': $trimmedtitle,
              'THUMBNAIL_URI': imageUrl = null ? '' : imageUrl,
              'LOCALE': $lookup(
                  [{
                          'en-US': 'English',
                          'en': 'English',
                          'fr': 'French',
                          'fr-FR': 'French',
                          'de': 'German',
                          'de-DE': 'German',
                          'es': 'Spanish',
                          'es-DO': 'Spanish'
                      }
                  ],
                  (localizedMetadata[0].localeCode ? localizedMetadata[0].localeCode : 'English')
              ),
              'LAUNCH_IN_A_NEW_BWSR_WINDOW': 'Y',
              'REV_DTE': (
                      $ms:= lifecycle.lastUpdatedDate ? $toMillis(lifecycle.lastUpdatedDate) : null;
                      $ms ? $fromMillis($ms, '[MNn,*-3]-[D01]-[Y0001] [H01]:[m01]:[s01][z]') : null;
                  ),
              'CPNT_SRC_ID': contentType.'source' = null ? '' : contentType.'source',
              'CREATE_DTE': (
                      $ms1:= lifecycle.publishDate ? $toMillis(lifecycle.publishDate) : null;
                          $ms1 ? $fromMillis($ms1, '[MNn,*-3]-[D01]-[Y0001] [H01]:[m01]:[s01][z]') : null;
                  ),
              'CONTACT_HRS': $duration_in_hours,
              'ENABLE_MOBILE_ACCESS': true,
              'MOBILE_PRIMARY_PARAM': link
          }
        );

		$transformed_data;
)
