$.{
	'username': userId,
	'assetid': contentUuid,
	'xapiactivityid': ($percipiotype := $lookup([{
					'video': 'video',
					'course': 'course',
					'channel': 'channel',
					'book': 'book',
					'book review': 'book',
					'audiobook': 'audiobook',
					'book summary': 'book',
					'audiobook summary': 'audiobook',
					'testprep': 'linked_content',
					'practice lab': 'linked_content',
					'linked content': 'linked_content'
				}
			], ($lowercase(contentType))); $trim('https://xapi.percipio.com/xapi/' & $percipiotype & '/' & contentUuid)),
	'duration_hhmmss': (
			$totalseconds := $number(duration ? duration : 0);
			$hours := $floor($totalseconds / 3600);
			$minutes := $floor($totalseconds % 3600 / 60);
			$seconds := $floor($totalseconds % 3600 % 60);
			$result := $pad($hours & '', -2, '0') & ':' & $pad($minutes & '', -2, '0') & ':' & $pad($seconds & '', -2, '0');
			$result
	),
	'status': ($coursestatus := $lookup([{
					'started': 'INCOMPLETE',
					'completed': 'COMPLETE'
				}
			], ($lowercase(status))); $coursestatus ? $coursestatus : 'INCOMPLETE'),
	'firstaccessdateYYYY-MM-DD': firstAccess ? $substringBefore(firstAccess, 'T') : '',
	'lastaccessdateYYYY-MM-DD': lastAccess ? $substringBefore(lastAccess, 'T') : '',
	'completeddateYYYY-MM-DD': completedDate ? $substringBefore(completedDate, 'T') : '',
	'firstscore': firstScore ? $number(firstScore) : null,
	'lastscore': lastScore ? $number(lastScore) : null,
	'highscore': highScore ? $number(highScore) : null,
	'accessCount': totalAccess ? $number(totalAccess) : null
}